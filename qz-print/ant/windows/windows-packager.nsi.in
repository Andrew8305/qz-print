!include MUI2.nsh
!include x64.nsh

Name "${socket.name}"
OutFile "${out.dir}\${build.socket.name}-${build.version}-setup.exe"

;-------------------------------

InstallDir "$PROGRAMFILES\${socket.name}"

;-------------------------------

!define MUI_ICON "${build.nsis.branding}\${build.windows.icon}"

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${basedir}\..\LICENSE.txt"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"

;------------------------------

Section
  ; Kills any running ${socket.name} processes
  ExecWait "wmic.exe process where $\"Name like '%java%' and CommandLine like '%${build.socket.name}.jar%'$\" call terminate"

  SetOutPath "$INSTDIR"

  File /r "${dist.socket.dir}\*"
  Delete "$INSTDIR\${build.linux.icon}"
  Delete "$INSTDIR\${build.apple.icon}"

  ; Exports a self-signed certificate and properties file
  File /r "${build.dir}\${build.windows.keygen.name}*"
  ExecWait "cscript.exe //NoLogo $\"$INSTDIR\${build.windows.keygen.name}$\""
  Delete "$INSTDIR\${build.windows.keygen.name}"

  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "DisplayName" "${socket.name} ${build.version}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                     "Publisher" "${vendor.company}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "DisplayIcon" "$INSTDIR\${build.windows.icon}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "HelpLink" "${vendor.website}/support"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "URLUpdateInfo" "${vendor.website}/download"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "URLInfoAbout" "${vendor.website}/support"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "DisplayVersion" "${build.version}"
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}" \
                   "EstimatedSize" "${build.socket.size}"

  WriteUninstaller "$INSTDIR\uninstall.exe"

  CreateShortCut "$SMPROGRAMS\${socket.name}.lnk" "$INSTDIR\${build.socket.name}.jar" "" "$INSTDIR\${build.windows.icon}" 0
SectionEnd

;-------------------------------

Section "Uninstall"
  ; Kills any running ${socket.name} processes
  ExecWait "wmic.exe process where $\"Name like '%java%' and CommandLine like '%${build.socket.name}.jar%'$\" call terminate"

  Delete "$SMPROGRAMS\${socket.name}.lnk"
  Delete "$INSTDIR\uninstall.exe"
  RMDir /r "$INSTDIR"

  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${socket.name}"

  Delete "$DESKTOP\${socket.name}.url"
  Delete "$DESKTOP\${socket.name}.lnk"

  ; Remove ${vendor.company} certificates
  ExecWait "certutil.exe -delstore $\"${build.windows.certstore}$\" $\"${vendor.company}$\"";
SectionEnd

;-------------------------------

Function .onInit
    ${If} ${RunningX64}
        SetRegView 64
        StrCpy $INSTDIR "$PROGRAMFILES64\${socket.name}"
    ${EndIf}
FunctionEnd